import Heights.BasicMultiset
/-!
# Instances of AdmissibleAbsValues

We provide instances of `Height.AdmissibleAbsValues` for

* algebraic number fields.

## TODO

* Fields of rational functions in `n` variables.

* Finite extensions of fields with `Height.AdmissibleAbsValues`.

-/

/-!
### Instance for number fields
-/

namespace NumberField

open Height

variable {K : Type*} [Field K]

/-- A predicate singling out infinite places among the absolute values on a number field `K`. -/
def IsInfinitePlace (w : AbsoluteValue K ‚Ñù) : Prop :=
  ‚àÉ œÜ : K ‚Üí+* ‚ÑÇ, place œÜ = w

lemma InfinitePlace.isInfinitePlace (v : InfinitePlace K) : IsInfinitePlace v.val := by
  simp [IsInfinitePlace, v.prop]

lemma isInfinitePlace_iff (v : AbsoluteValue K ‚Ñù) :
    IsInfinitePlace v ‚Üî ‚àÉ w : InfinitePlace K, w.val = v :=
  ‚ü®fun H ‚Ü¶ ‚ü®‚ü®v, H‚ü©, rfl‚ü©, fun ‚ü®w, hw‚ü© ‚Ü¶ hw ‚ñ∏ w.isInfinitePlace‚ü©

variable  [NumberField K]

/-- A predicate singling out finite places among the absolute values on a number field `K`. -/
def IsFinitePlace (w : AbsoluteValue K ‚Ñù) : Prop :=
  ‚àÉ v : IsDedekindDomain.HeightOneSpectrum (ùìû K), place (FinitePlace.embedding v) = w

lemma FinitePlace.isFinitePlace (v : FinitePlace K) : IsFinitePlace v.val := by
  simp [IsFinitePlace, v.prop]

lemma isFinitePlace_iff (v : AbsoluteValue K ‚Ñù) :
    IsFinitePlace v ‚Üî ‚àÉ w : FinitePlace K, w.val = v :=
  ‚ü®fun H ‚Ü¶ ‚ü®‚ü®v, H‚ü©, rfl‚ü©, fun ‚ü®w, hw‚ü© ‚Ü¶ hw ‚ñ∏ w.isFinitePlace‚ü©

lemma FinitePlace.coe_apply (v : FinitePlace K) (x : K) : v x = v.val x := rfl

variable (K) in
/-- The infinite places of a number field `K` as a `Multiset` of absolute values on `K`,
with multiplicity given by `InfinitePlace.mult`. -/
noncomputable def multisetInfinitePlace : Multiset (AbsoluteValue K ‚Ñù) :=
  .bind (.univ : Finset (InfinitePlace K)).val fun v ‚Ü¶ .replicate v.mult v.val

@[simp]
lemma mem_multisetInfinitePlace {v : AbsoluteValue K ‚Ñù} :
    v ‚àà multisetInfinitePlace K ‚Üî IsInfinitePlace v := by
  simp [multisetInfinitePlace, Multiset.mem_replicate, isInfinitePlace_iff, eq_comm (a := v)]

lemma count_multisetInfinitePlace_eq_mult [DecidableEq (AbsoluteValue K ‚Ñù)]
    [DecidableEq (InfinitePlace K)] (v : InfinitePlace K) :
    (multisetInfinitePlace K).count v.val = v.mult := by
  simpa only [multisetInfinitePlace, Multiset.count_bind, Finset.sum_map_val,
    Multiset.count_replicate, ‚Üê Subtype.ext_iff] using Fintype.sum_ite_eq' v ..

-- For the user-facing version, see `prod_archAbsVal_eq` below.
variable (K) in
private lemma prod_multisetInfinitePlace_eq {M : Type*} [CommMonoid M] {f : AbsoluteValue K ‚Ñù ‚Üí M} :
    ((multisetInfinitePlace K).map f).prod = ‚àè v : InfinitePlace K, f v.val ^ v.mult := by
  classical
  rw [Finset.prod_multiset_map_count]
  exact Finset.prod_bij' (fun w hw ‚Ü¶ ‚ü®w, mem_multisetInfinitePlace.mp <| Multiset.mem_dedup.mp hw‚ü©)
    (fun v _ ‚Ü¶ v.val) (fun _ _ ‚Ü¶ Finset.mem_univ _) (fun v _ ‚Ü¶ by simp [v.isInfinitePlace])
    (fun _ _ ‚Ü¶ rfl) (fun _ _ ‚Ü¶ rfl) fun w hw ‚Ü¶ by rw [count_multisetInfinitePlace_eq_mult ‚ü®w, _‚ü©]

noncomputable
instance instAdmissibleAbsValues : AdmissibleAbsValues K where
  archAbsVal := multisetInfinitePlace K
  nonarchAbsVal := {v | IsFinitePlace v}
  isNonarchimedean v hv := FinitePlace.add_le ‚ü®v, by simpa using hv‚ü©
  mulSupport_finite := FinitePlace.mulSupport_finite
  product_formula {x} hx := prod_multisetInfinitePlace_eq (M := ‚Ñù) K ‚ñ∏ prod_abs_eq_one hx

lemma prod_archAbsVal_eq {M : Type*} [CommMonoid M] (f : AbsoluteValue K ‚Ñù ‚Üí M) :
    (AdmissibleAbsValues.archAbsVal.map f).prod = ‚àè v : InfinitePlace K, f v.val ^ v.mult :=
  prod_multisetInfinitePlace_eq K

lemma prod_nonarchAbsVal_eq {M : Type*} [CommMonoid M] (f : AbsoluteValue K ‚Ñù ‚Üí M) :
    (‚àè·∂† v : AdmissibleAbsValues.nonarchAbsVal, f v.val) = ‚àè·∂† v : FinitePlace K, f v.val :=
  rfl

/-- This is the familiar definition of the multiplicative height on a number field. -/
lemma mulHeight‚ÇÅ_eq (x : K) :
    mulHeight‚ÇÅ x =
      (‚àè v : InfinitePlace K, max (v x) 1 ^ v.mult) * ‚àè·∂† v : FinitePlace K, max (v x) 1 := by
  simp only [FinitePlace.coe_apply, InfinitePlace.coe_apply, mulHeight‚ÇÅ, prod_archAbsVal_eq,
    prod_nonarchAbsVal_eq (f := fun v ‚Ü¶ max (v x) 1)]

/-- This is the familiar definition of the multiplicative height on tuples
of number field elements. -/
lemma mulHeight_eq {Œπ : Type*} (x : Œπ ‚Üí K) :
    mulHeight x =
      (‚àè v : InfinitePlace K, (‚®Ü i, v (x i)) ^ v.mult) * ‚àè·∂† v : FinitePlace K, ‚®Ü i, v (x i) := by
  simp only [FinitePlace.coe_apply, InfinitePlace.coe_apply, mulHeight, prod_archAbsVal_eq,
    prod_nonarchAbsVal_eq (f := fun v ‚Ü¶ ‚®Ü i, v (x i))]

end NumberField
