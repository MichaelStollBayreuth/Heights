import Heights.Instances

/-!
### Heights over ‚Ñö
-/

open NumberField

section API

-- The following should go into `Mathlib.NumberTheory.NumberField.Embeddings`

instance : TrivialStar (‚Ñö ‚Üí+* ‚ÑÇ) := { star_trivial r := by ext1; simp only [eq_ratCast] }

@[simp]
lemma Rat.prod_infinitePlace {M : Type*} [CommMonoid M] (f : InfinitePlace ‚Ñö ‚Üí M) :
    ‚àè v, f v = f Rat.infinitePlace := by
  simp only [‚Üê Finset.singleton_eq_univ Rat.infinitePlace, Finset.prod_singleton]

-- (up to here)

-- The following are not needed, after all, but might be useful eventually.
/-
lemma ciSup_natCast {Œπ R : Type*} [Fintype Œπ] [Nonempty Œπ] [ConditionallyCompleteLinearOrder R]
    [TopologicalSpace R] [OrderClosedTopology R] [AddMonoidWithOne R] [AddLeftMono R]
    [ZeroLEOneClass R] (f : Œπ ‚Üí ‚Ñï) :
    ‚®Ü i, (f i : R) = (‚®Ü i, f i : ) := by
  -- not found by `apply?`
  refine (Monotone.map_ciSup_of_continuousAt ?_ Nat.mono_cast (Finite.bddAbove_range f)).symm
  exact continuous_of_discreteTopology.continuousAt -/

/-
lemma ciInf_natCast {Œπ R : Type*} [Fintype Œπ] [Nonempty Œπ] [ConditionallyCompleteLinearOrder R]
    [TopologicalSpace R] [OrderClosedTopology R] [AddMonoidWithOne R] [AddLeftMono R]
    [ZeroLEOneClass R] (f : Œπ ‚Üí ‚Ñï) :
    ‚®Ö i, (f i : R) = (‚®Ö i, f i : ) := by
  refine (Monotone.map_ciInf_of_continuousAt ?_ Nat.mono_cast (Finite.bddBelow_range f)).symm
  exact continuous_of_discreteTopology.continuousAt -/

/-
lemma Real.iSup_inv_eq_iInf {Œπ : Type*} [Fintype Œπ] [Nonempty Œπ] {f : Œπ ‚Üí ‚Ñù} (hf : ‚àÄ i, 0 < f i) :
    ‚®Ü i, (f i)‚Åª¬π = (‚®Ö i, f i)‚Åª¬π := by
  have ‚ü®i, hi‚ü© := exists_eq_ciInf_of_finite (f := f)
  have H‚ÇÄ : 0 < ‚®Ö i, f i := hi ‚ñ∏ hf i
  refine le_antisymm ?_ ?_
  ¬∑ exact ciSup_le fun i ‚Ü¶ (inv_le_inv‚ÇÄ (hf i) H‚ÇÄ).mpr <| ciInf_le (Finite.bddBelow_range _) i
  ¬∑ exact le_ciSup_of_le (Finite.bddAbove_range _) i <| by rw [‚Üê hi] -/

-- The following material could go into `Mathlib.RingTheory.DedekindDomain.Ideal`

open Ideal

lemma Int.heightOneSpectrum_aux‚ÇÅ (I : IsDedekindDomain.HeightOneSpectrum ‚Ñ§) :
    span {((Submodule.IsPrincipal.principal I.asIdeal).choose.natAbs : ‚Ñ§)} = I.asIdeal := by
  have := (Submodule.IsPrincipal.principal I.asIdeal).choose_spec
  rw [submodule_span_eq] at this
  rw [this, span_natAbs]
  convert rfl

lemma Int.heightOneSpectrum_aux‚ÇÇ (p : Nat.Primes) :
    (Submodule.IsPrincipal.principal <| span {(p.val : ‚Ñ§)}).choose.natAbs = p := by
  have := (Submodule.IsPrincipal.principal <| span {(p.val : ‚Ñ§)}).choose_spec
  rw [submodule_span_eq, span_singleton_eq_span_singleton, associated_iff_natAbs] at this
  rw [‚Üê this, natAbs_cast]

/-- The canonical bijection between the set of prime numbers and the height one spectrum of `‚Ñ§` -/
noncomputable
def Int.natPrimesEquivHeightOneSpectrum : Nat.Primes ‚âÉ IsDedekindDomain.HeightOneSpectrum ‚Ñ§ where
  toFun p := .mk (span {(p.val : ‚Ñ§)})
    ((span_singleton_prime <| mod_cast p.prop.ne_zero).mpr <| Nat.prime_iff_prime_int.mp p.prop)
    (by simp only [ne_eq, span_singleton_eq_bot, Nat.cast_eq_zero]
        exact_mod_cast p.prop.ne_zero)
  invFun I :=
    ‚ü®(Submodule.IsPrincipal.principal I.asIdeal).choose.natAbs, by
      have := (Submodule.IsPrincipal.principal I.asIdeal).choose_spec
      have h : (Submodule.IsPrincipal.principal I.asIdeal).choose ‚â† 0 := by
        intro hf
        rw [hf, submodule_span_eq, span_singleton_eq_bot.mpr rfl] at this
        exact I.ne_bot this
      rw [‚Üê Int.prime_iff_natAbs_prime, ‚Üê span_singleton_prime h, ‚Üê submodule_span_eq, ‚Üê this]
      exact I.isPrime‚ü©
  left_inv p := Subtype.ext <| heightOneSpectrum_aux‚ÇÇ p
  right_inv I := IsDedekindDomain.HeightOneSpectrum.ext_iff.mpr <| heightOneSpectrum_aux‚ÇÅ I

/-- If `v` is an element of the height one spectrum of `‚Ñ§`, then membership in the associated
ideal is equivalent to divisibility by the corresponding prime number. -/
lemma IsDedekindDomain.HeightOneSpectrum.mem_iff_dvd (v : HeightOneSpectrum ‚Ñ§) (x : ‚Ñ§) :
    x ‚àà v.asIdeal ‚Üî (Int.natPrimesEquivHeightOneSpectrum.symm v : ‚Ñ§) ‚à£ x := by
  have : v.asIdeal = span {(Int.natPrimesEquivHeightOneSpectrum.symm v : ‚Ñ§)} := by
    simp only [Int.natPrimesEquivHeightOneSpectrum, submodule_span_eq, Equiv.coe_fn_symm_mk]
    have := (Submodule.IsPrincipal.principal v.asIdeal).choose_spec
    rw [submodule_span_eq] at this
    exact this.trans <| span_singleton_eq_span_singleton.mpr <| Int.associated_natAbs _
  simpa only [this] using mem_span_singleton

/-- A ring isomorphism `R ‚Üí S` induces a map from the height one spectrum of `R` to that of `S`. -/
def RingEquiv.mapIsDedekindDomainHeightOneSpectrum {R S : Type*} [CommRing R] [IsDedekindDomain R]
    [CommRing S] [IsDedekindDomain S] (e : R ‚âÉ+* S) (v : IsDedekindDomain.HeightOneSpectrum R) :
    IsDedekindDomain.HeightOneSpectrum S :=
  .mk (map e v.asIdeal)
      (by have := v.isPrime
          exact map_isPrime_of_equiv e (I := v.asIdeal))
      (by have := v.ne_bot
          contrapose! this
          rwa [map_eq_bot_iff_of_injective <| RingEquiv.injective e] at this)

/-- A ring isomorphism (of Dedekind domains) induces an equivalence
between the height one spectra. -/
def RingEquiv.isDedekindDomainHeightOneSpectrumEquiv {R S : Type*} [CommRing R]
    [IsDedekindDomain R] [CommRing S] [IsDedekindDomain S] (e : R ‚âÉ+* S) :
    IsDedekindDomain.HeightOneSpectrum R ‚âÉ IsDedekindDomain.HeightOneSpectrum S where
      toFun := e.mapIsDedekindDomainHeightOneSpectrum
      invFun := e.symm.mapIsDedekindDomainHeightOneSpectrum
      left_inv v := by
        simp only [mapIsDedekindDomainHeightOneSpectrum, map_symm]
        exact IsDedekindDomain.HeightOneSpectrum.ext <| comap_map_of_bijective e e.bijective
      right_inv v := by
        simp only [mapIsDedekindDomainHeightOneSpectrum, map_symm]
        exact IsDedekindDomain.HeightOneSpectrum.ext <|
          map_comap_of_surjective e e.surjective _

-- (up to here)

end API

section tuples

open Ideal

-- We need to increase the priority to avoid an instance mismatch below:
-- lemma test (v : IsDedekindDomain.HeightOneSpectrum (RingOfIntegers ‚Ñö)) (x : RingOfIntegers ‚Ñö) :
--     ‚Äñ(embedding v) x‚Äñ = 1 ‚Üî x ‚àâ v.asIdeal := by
--   convert NumberField.norm_eq_one_iff_not_mem v x
--   -- UniformSpace.Completion.instNorm ‚Ñö = NormedField.toNorm
--   sorry
-- attribute [local instance 2000] NormedField.toNorm -- apparently no longer necessary

/-- The term corresponding to a finite place in the definition of the multiplicative height
of a tuple of rational numbers equals `1` if the tuple consists of coprime integers. -/
lemma Rat.iSup_finitePlace_apply_eq_one_of_gcd_eq_one (v : FinitePlace ‚Ñö) {Œπ : Type*}
    [Fintype Œπ] [Nonempty Œπ] {x : Œπ ‚Üí ‚Ñ§} (hx : Finset.univ.gcd x = 1) :
    ‚®Ü i, v (x i) = 1 := by
  let v' : IsDedekindDomain.HeightOneSpectrum (ùìû ‚Ñö) := v.maximalIdeal
  have ‚ü®i, hi‚ü© : ‚àÉ i, ‚Äñ(FinitePlace.embedding v') (Rat.ringOfIntegersEquiv.symm (x i) : ‚Ñö)‚Äñ = 1 := by
    simp_rw [FinitePlace.norm_eq_one_iff_notMem]
    by_contra! H
    let pI := Rat.ringOfIntegersEquiv.isDedekindDomainHeightOneSpectrumEquiv v'
    let p := Int.natPrimesEquivHeightOneSpectrum.symm pI
    have h i : (p : ‚Ñ§) ‚à£ x i := by
      rw [‚Üê pI.mem_iff_dvd, show pI.asIdeal = .map Rat.ringOfIntegersEquiv v'.asIdeal from rfl,
        mem_map_of_equiv]
      exact ‚ü®_, H i, RingEquiv.apply_symm_apply ..‚ü©
    refine p.prop.not_dvd_one ?_
    rw [‚Üê Int.ofNat_dvd, Nat.cast_one, ‚Üê hx]
    exact Finset.dvd_gcd fun i _ ‚Ü¶ h i
  have H i : (x i : ‚Ñö) = Rat.ringOfIntegersEquiv.symm (x i) := by
    simp only [eq_intCast, map_intCast]
  simp_rw [H, ‚Üê FinitePlace.norm_embedding_eq]
  exact le_antisymm (Real.iSup_le (fun i ‚Ü¶ FinitePlace.norm_le_one v' _) zero_le_one) <|
    le_ciSup_of_le (Finite.bddAbove_range _) i hi.symm.le

open Height

open AdmissibleAbsValues in
/-- The multiplicative height of a tuple of rational numbers that consists of coprime integers
is the maximum of the absolute values of the entries. -/
lemma Rat.mulHeight_eq_max_abs_of_gcd_eq_one {Œπ : Type*} [Fintype Œπ] [Nonempty Œπ] {x : Œπ ‚Üí ‚Ñ§}
    (hx : Finset.univ.gcd x = 1) :
    mulHeight (((‚Üë) : ‚Ñ§ ‚Üí  ‚Ñö) ‚àò x) = ‚®Ü i, |x i| := by
  simp only [mulHeight]
  conv_rhs => rw [‚Üê mul_one ((‚®Ü i, |x i| :) : ‚Ñù)]
  congr 1
  ¬∑ simp only [ArchAbsVal, archAbsVal, Function.comp_apply, weight, InfinitePlace.mult, pow_ite,
      pow_one, prod_infinitePlace, isReal_infinitePlace, ‚ÜìreduceIte]
    have (i : Œπ) : Rat.infinitePlace.val (x i) = Rat.infinitePlace (x i) := rfl
    conv => enter [1, 1, i]; rw [this, Rat.infinitePlace_apply]
    exact_mod_cast (Monotone.map_ciSup_of_continuousAt continuous_of_discreteTopology.continuousAt
      Int.cast_mono (Finite.bddAbove_range _)).symm
  ¬∑ exact finprod_eq_one_of_forall_eq_one
      fun v ‚Ü¶ Rat.iSup_finitePlace_apply_eq_one_of_gcd_eq_one v hx

/-- The multiplicative height of a tuple of rational numbers that consists of coprime integers
is the maximum of the absolute values of the entries. This version is in terms of a subtype. -/
lemma Rat.mulHeight_eq_max_abs_of_gcd_eq_one' {Œπ : Type*} [Fintype Œπ] [Nonempty Œπ]
    (x : { x : Œπ ‚Üí ‚Ñ§ // Finset.univ.gcd x = 1 }) :
    mulHeight (((‚Üë) : ‚Ñ§ ‚Üí  ‚Ñö) ‚àò x.val) = ‚®Ü i, |x.val i| :=
  mulHeight_eq_max_abs_of_gcd_eq_one x.prop

end tuples

section projective

variable {Œπ : Type*} [Fintype Œπ]

private
lemma Int.ne_zero_of_gcd_eq_one {x : Œπ ‚Üí ‚Ñ§} (hx : Finset.univ.gcd x = 1) :
    ((‚Üë) : ‚Ñ§ ‚Üí ‚Ñö) ‚àò x ‚â† 0 := by
  refine (Function.comp_ne_zero_iff _ Rat.intCast_injective rfl).mpr fun h ‚Ü¶ ?_
  exact one_ne_zero (hx.symm.trans <| Finset.gcd_eq_zero_iff.mpr fun i _ ‚Ü¶  congrFun h i)

/-- The map sending a coprime integer tuple to the corresponding point in `‚Ñô‚Åø(‚Ñö)`. -/
def Rat.coprimeIntToProjective :
    { x : Œπ ‚Üí ‚Ñ§ // Finset.univ.gcd x = 1 } ‚Üí Projectivization ‚Ñö (Œπ ‚Üí ‚Ñö) :=
  fun x ‚Ü¶ .mk ‚Ñö (((‚Üë) : ‚Ñ§ ‚Üí ‚Ñö) ‚àò x.val) (Int.ne_zero_of_gcd_eq_one x.prop)

lemma Rat.exists_int_mul_eq_cast (r : ‚Ñö) (n : ‚Ñï) : (‚àÉ z : ‚Ñ§, n * r = z) ‚Üî r.den ‚à£ n := by
  have hr‚ÇÄ : (r.den : ‚Ñö) ‚â† 0 := Nat.cast_ne_zero.mpr r.pos.ne'
  nth_rewrite 1 [‚Üê Rat.num_div_den r, ‚Üê mul_div_assoc]
  refine ‚ü®fun ‚ü®z, hz‚ü© ‚Ü¶ ?_, fun ‚ü®a, ha‚ü© ‚Ü¶ ‚ü®a * r.num, ?_‚ü©‚ü©
  ¬∑ rw [div_eq_iff hr‚ÇÄ] at hz
    exact Int.ofNat_dvd.mp <| (isCoprime_num_den r).symm.dvd_of_dvd_mul_right
      ‚ü®_, mod_cast mul_comm (z : ‚Ñö) _ ‚ñ∏ hz‚ü©
  ¬∑ rw [ha, Int.cast_mul, Nat.cast_mul, Int.cast_natCast, mul_assoc, mul_comm, mul_div_assoc,
      div_self hr‚ÇÄ, mul_one]

/-- Every point in `‚Ñô‚Åø(‚Ñö)` has a representative with coprime integral entries. -/
lemma Rat.coprimeIntToProjective_surjective [Nonempty Œπ] :
    Function.Surjective (coprimeIntToProjective (Œπ := Œπ)) := by
  intro x
  let r := x.rep
  let d := Finset.univ.lcm (Rat.den ‚àò r)
  have hr : d ‚Ä¢ r ‚â† 0 := by
    refine smul_ne_zero (fun hd ‚Ü¶ ?_) x.rep_nonzero
    rw [Finset.lcm_eq_zero_iff] at hd
    simp at hd
  have ‚ü®R, hR‚ü© : ‚àÉ z : Œπ ‚Üí ‚Ñ§, d ‚Ä¢ r = ((‚Üë) : ‚Ñ§ ‚Üí ‚Ñö) ‚àò z := by
    have (i : Œπ) : ‚àÉ y : ‚Ñ§, (d ‚Ä¢ r) i = y := by
      simp only [nsmul_eq_mul, Pi.natCast_def, Pi.mul_apply, d]
      exact (exists_int_mul_eq_cast ..).mpr <| Finset.dvd_lcm (Finset.mem_univ i)
    exact ‚ü®fun i ‚Ü¶ (this i).choose, funext fun i ‚Ü¶ (this i).choose_spec‚ü©
  have hR‚ÇÄ : R ‚â† 0 := by
    contrapose! hr
    simpa only [hr, Pi.comp_zero, Int.cast_zero, Function.const_zero] using hR
  have ‚ü®R', hR‚ÇÅ, hR‚ÇÇ‚ü© := Finset.extract_gcd R Finset.univ_nonempty
  have hg : Finset.univ.gcd R ‚â† 0 := by
    rw [ne_eq, Finset.gcd_eq_zero_iff]
    push_neg
    simpa only [Finset.mem_univ, true_and] using Function.ne_iff.mp hR‚ÇÄ
  have hR‚ÇÉ : ((‚Üë) : ‚Ñ§ ‚Üí ‚Ñö) ‚àò R' = ((d : ‚Ñö) / Finset.univ.gcd R) ‚Ä¢ r := by
    have : R = Finset.univ.gcd R ‚Ä¢ R' := by
      simp only [Finset.mem_univ, forall_const] at hR‚ÇÅ
      exact funext hR‚ÇÅ
    rw [div_eq_mul_inv, mul_comm, ‚Üê smul_smul, eq_inv_smul_iff‚ÇÄ <| mod_cast hg,
      Int.cast_smul_eq_zsmul, Nat.cast_smul_eq_nsmul, hR, this]
    ext1
    simp only [zsmul_eq_mul, Pi.intCast_def, Int.cast_id, Pi.smul_apply, Function.comp_apply,
      Pi.mul_apply, Int.cast_mul]
    congr
    ext1
    simp only [Pi.mul_apply, Finset.mem_univ, hR‚ÇÅ]
  refine ‚ü®‚ü®R', hR‚ÇÇ‚ü©, ?_‚ü©
  simp only [coprimeIntToProjective]
  rw [‚Üê x.mk_rep]
  refine (Projectivization.mk_eq_mk_iff ‚Ñö _ _ (Int.ne_zero_of_gcd_eq_one hR‚ÇÇ) x.rep_nonzero).mpr ?_
  have ha : (d : ‚Ñö) / Finset.univ.gcd R ‚â† 0 := by
    refine div_ne_zero (fun hd ‚Ü¶ ?_) <| mod_cast hg
    norm_cast at hd
    rw [Finset.lcm_eq_zero_iff] at hd
    simp at hd
  exact ‚ü®ha.isUnit.unit, hR‚ÇÉ.symm‚ü©

open Height

lemma Rat.mulHeight_coprimeIntToProjective_eq (x : { x : Œπ ‚Üí ‚Ñ§ // Finset.univ.gcd x = 1 }) :
    mulHeight (((‚Üë) : ‚Ñ§ ‚Üí ‚Ñö) ‚àò x.val) = (coprimeIntToProjective x).mulHeight := rfl

/-- The height on `‚Ñô‚Åø(‚Ñö)` satisfies the *Northcott Property*: there are only finitely many
points of bounded (multiplicative) height. -/
lemma Projectivization.Rat.finite_of_mulHeight_le [Nonempty Œπ] (B : ‚Ñù) :
    {x : Projectivization ‚Ñö (Œπ ‚Üí ‚Ñö) | x.mulHeight ‚â§ B}.Finite := by
  let s := {x : { x : Œπ ‚Üí ‚Ñ§  // Finset.univ.gcd x = 1 } |
    Height.mulHeight (((‚Üë) : ‚Ñ§ ‚Üí ‚Ñö) ‚àò  x.val) ‚â§ B}
  have hs : s.Finite := by
    simp only [s, Rat.mulHeight_eq_max_abs_of_gcd_eq_one']
    refine Set.Finite.of_finite_image ?_ Set.injOn_subtype_val
    refine Set.Finite.subset (s := {x | ‚àÄ i, |x i| ‚â§ B}) ?_ fun x hx ‚Ü¶ ?_
    ¬∑ refine Set.forall_finite_image_eval_iff.mp fun i ‚Ü¶ ?_
      simp only [Function.eval, Set.image]
      have : {z | ‚àÉ a ‚àà {x : Œπ ‚Üí ‚Ñ§ | ‚àÄ (i : Œπ), |(x i : ‚Ñù)| ‚â§ B}, a i = z} ‚äÜ {z | |(z : ‚Ñù)| ‚â§ B} := by
        intro z hz
        obtain ‚ü®a, ha, rfl‚ü© := Set.mem_setOf.mp hz
        exact ha i
      refine Set.Finite.subset ?_ this
      simpa only [abs_le, ‚Üê Int.ceil_le, ‚Üê Int.le_floor, Set.Icc_def] using Set.finite_Icc ..
    ¬∑ simp only [Set.mem_image, Set.mem_setOf_eq, Subtype.exists, exists_and_left, exists_prop,
        exists_eq_right_right] at hx -- `‚Üë(‚®Ü i, |x i|) ‚â§ B ‚àß Finset.univ.gcd x = 1`
      have : ((‚®Ü i, |x i| :) : ‚Ñù) = ‚®Ü i, (|x i| : ‚Ñù) := by
        conv => enter [2, 1, i]; rw [‚Üê Int.cast_abs]
        -- this incantation looks a bit ridiculous, but I've found nothing simpler
        exact Monotone.map_ciSup_of_continuousAt continuous_of_discreteTopology.continuousAt
          Int.cast_mono (Finite.bddAbove_range _)
      exact (ciSup_le_iff <| Finite.bddAbove_range _).mp <| this ‚ñ∏ hx.1
  refine Set.Finite.of_surjOn Rat.coprimeIntToProjective (fun x hx ‚Ü¶ ?_) hs
  ¬∑ rw [Set.mem_image, Subtype.exists]
    have ‚ü®y, hy‚ü© := Rat.coprimeIntToProjective_surjective x
    exact ‚ü®y.val, y.prop,
      by simp [s, Rat.mulHeight_coprimeIntToProjective_eq, hy, Set.mem_setOf.mp hx], hy‚ü©

/-- The height on `‚Ñô‚Åø(‚Ñö)` satisfies the *Northcott Property*: there are only finitely many
points of bounded (logarithmic) height. -/
lemma Projectivization.Rat.finite_of_logHeight_le [Nonempty Œπ] (B : ‚Ñù) :
    {x : Projectivization ‚Ñö (Œπ ‚Üí ‚Ñö) | x.logHeight ‚â§ B}.Finite := by
  have H (x : Projectivization ‚Ñö (Œπ ‚Üí ‚Ñö)) : x.logHeight ‚â§ B ‚Üî x.mulHeight ‚â§ B.exp := by
    rw [x.logHeight_eq_log_mulHeight]
    exact Real.log_le_iff_le_exp x.mulHeight_pos
  simpa only [H] using finite_of_mulHeight_le (Real.exp B)

/-- The map from a field `K` to the projectivization of a two-dimensional `K`-vector space
given by sending `x` to `(x : 1)`. -/
-- better namespace than `_root_`?
def toProjectivization {K : Type*} [Field K] (x : K) : Projectivization K (Fin 2 ‚Üí K) :=
  .mk K ![x, 1] <| by simp

lemma toProjectivization_injective {K : Type*} [Field K] :
    Function.Injective (toProjectivization (K := K)) := by
  intro x y h
  simp only [toProjectivization, Projectivization.mk_eq_mk_iff', Matrix.smul_cons, smul_eq_mul,
    mul_one, Matrix.smul_empty] at h
  have ‚ü®a, ha‚ü© := h
  have ha‚ÇÄ := congrFun ha 0
  have ha‚ÇÅ := congrFun ha 1
  simp only [Fin.isValue, Matrix.cons_val_zero] at ha‚ÇÄ
  simp only [Fin.isValue, Matrix.cons_val_one, Matrix.cons_val_fin_one] at ha‚ÇÅ
  simpa [ha‚ÇÅ] using ha‚ÇÄ.symm

/-- The height on `‚Ñö` satisfies the *Northcott Property*: there are only finitely many
points of bounded (multiplicative) height. -/
lemma Rat.finite_of_mulHeight_le (B : ‚Ñù) : {x : ‚Ñö | mulHeight‚ÇÅ x ‚â§ B}.Finite := by
  refine Set.Finite.of_finite_image ?_ toProjectivization_injective.injOn
  simp_rw [Set.image, mulHeight‚ÇÅ_eq_mulHeight, Set.mem_setOf]
  have : {x | ‚àÉ a : ‚Ñö, mulHeight ![a, 1] ‚â§ B ‚àß toProjectivization a = x} ‚äÜ
      {x | x.mulHeight ‚â§ B} := by
    intro x hx
    simp only [Set.mem_setOf] at hx ‚ä¢
    obtain ‚ü®a, ha, rfl‚ü© := hx
    exact ha
  exact Set.Finite.subset (Projectivization.Rat.finite_of_mulHeight_le B) this

/-- The height on `‚Ñö` satisfies the *Northcott Property*: there are only finitely many
points of bounded (logarithmic) height. -/
lemma Rat.finite_of_logHeight_le (B : ‚Ñù) : {x : ‚Ñö | logHeight‚ÇÅ x ‚â§ B}.Finite := by
  simpa only [logHeight‚ÇÅ, Real.log_le_iff_le_exp <| mulHeight‚ÇÅ_pos _]
    using finite_of_mulHeight_le (Real.exp B)

end projective
